src = files(
  'asn1/a_bitstr.c',
  'asn1/a_bool.c',
  'asn1/a_d2i_fp.c',
  'asn1/a_dup.c',
  'asn1/a_gentm.c',
  'asn1/a_i2d_fp.c',
  'asn1/a_int.c',
  'asn1/a_mbstr.c',
  'asn1/a_object.c',
  'asn1/a_octet.c',
  'asn1/a_strex.c',
  'asn1/a_strnid.c',
  'asn1/a_time.c',
  'asn1/a_type.c',
  'asn1/a_utctm.c',
  'asn1/a_utf8.c',
  'asn1/asn1_lib.c',
  'asn1/asn1_par.c',
  'asn1/asn_pack.c',
  'asn1/f_int.c',
  'asn1/f_string.c',
  'asn1/posix_time.c',
  'asn1/tasn_dec.c',
  'asn1/tasn_enc.c',
  'asn1/tasn_fre.c',
  'asn1/tasn_new.c',
  'asn1/tasn_typ.c',
  'asn1/tasn_utl.c',
  'base64/base64.c',
  'bio/bio.c',
  'bio/bio_addr.c',
  'bio/bio_mem.c',
  'bio/connect.c',
  'bio/dgram.c',
  'bio/errno.c',
  'bio/fd.c',
  'bio/file.c',
  'bio/hexdump.c',
  'bio/md.c',
  'bio/pair.c',
  'bio/printf.c',
  'bio/socket.c',
  'bio/socket_helper.c',
  'blake2/blake2.c',
  'bn_extra/bn_asn1.c',
  'bn_extra/convert.c',
  'buf/buf.c',
  'bytestring/asn1_compat.c',
  'bytestring/ber.c',
  'bytestring/cbb.c',
  'bytestring/cbs.c',
  'bytestring/unicode.c',
  'chacha/chacha.c',
  'cipher_extra/cipher_extra.c',
  'cipher_extra/derive_key.c',
  'cipher_extra/e_aes_cbc_hmac_sha1.c',
  'cipher_extra/e_aes_cbc_hmac_sha256.c',
  'cipher_extra/e_aesctrhmac.c',
  'cipher_extra/e_aesgcmsiv.c',
  'cipher_extra/e_chacha20poly1305.c',
  'cipher_extra/e_des.c',
  'cipher_extra/e_null.c',
  'cipher_extra/e_rc2.c',
  'cipher_extra/e_rc4.c',
  'cipher_extra/e_tls.c',
  'cipher_extra/tls_cbc.c',
  'conf/conf.c',
  'console/console.c',
  'crypto.c',
  'decrepit/bio/base64_bio.c',
  'decrepit/blowfish/blowfish.c',
  'decrepit/cast/cast.c',
  'decrepit/cast/cast_tables.c',
  'decrepit/cfb/cfb.c',
  'decrepit/dh/dh_decrepit.c',
  'decrepit/evp/evp_do_all.c',
  'decrepit/obj/obj_decrepit.c',
  'decrepit/ripemd/ripemd.c',
  'decrepit/rsa/rsa_decrepit.c',
  'decrepit/x509/x509_decrepit.c',
  'des/des.c',
  'dh_extra/dh_asn1.c',
  'dh_extra/params.c',
  'digest_extra/digest_extra.c',
  'dsa/dsa.c',
  'dsa/dsa_asn1.c',
  'ec_extra/ec_asn1.c',
  'ec_extra/ec_derive.c',
  'ec_extra/hash_to_curve.c',
  'ecdh_extra/ecdh_extra.c',
  'ecdsa_extra/ecdsa_asn1.c',
  'engine/engine.c',
  'err/err.c',
  'evp_extra/evp_asn1.c',
  'evp_extra/p_dh.c',
  'evp_extra/p_dh_asn1.c',
  'evp_extra/p_dsa.c',
  'evp_extra/p_dsa_asn1.c',
  'evp_extra/p_ec_asn1.c',
  'evp_extra/p_ed25519_asn1.c',
  'evp_extra/p_hmac_asn1.c',
  'evp_extra/p_kem_asn1.c',
  'evp_extra/p_methods.c',
  'evp_extra/p_pqdsa_asn1.c',
  'evp_extra/p_rsa_asn1.c',
  'evp_extra/p_x25519.c',
  'evp_extra/p_x25519_asn1.c',
  'evp_extra/print.c',
  'evp_extra/scrypt.c',
  'evp_extra/sign.c',
  'ex_data.c',
  'hpke/hpke.c',
  'hrss/hrss.c',
  'lhash/lhash.c',
  'mem.c',
  'obj/obj.c',
  'obj/obj_xref.c',
  'ocsp/ocsp_asn.c',
  'ocsp/ocsp_client.c',
  'ocsp/ocsp_extension.c',
  'ocsp/ocsp_http.c',
  'ocsp/ocsp_lib.c',
  'ocsp/ocsp_print.c',
  'ocsp/ocsp_server.c',
  'ocsp/ocsp_verify.c',
  'pem/pem_all.c',
  'pem/pem_info.c',
  'pem/pem_lib.c',
  'pem/pem_oth.c',
  'pem/pem_pk8.c',
  'pem/pem_pkey.c',
  'pem/pem_x509.c',
  'pem/pem_xaux.c',
  'pkcs7/bio/cipher.c',
  'pkcs7/pkcs7.c',
  'pkcs7/pkcs7_asn1.c',
  'pkcs7/pkcs7_x509.c',
  'pkcs8/p5_pbev2.c',
  'pkcs8/pkcs8.c',
  'pkcs8/pkcs8_x509.c',
  'poly1305/poly1305.c',
  'poly1305/poly1305_arm.c',
  'poly1305/poly1305_vec.c',
  'pool/pool.c',
  'rand_extra/ccrandomgeneratebytes.c',
  'rand_extra/deterministic.c',
  'rand_extra/getentropy.c',
  'rand_extra/rand_extra.c',
  'rand_extra/urandom.c',
  'rand_extra/vm_ube_fallback.c',
  'rand_extra/windows.c',
  'rc4/rc4.c',
  'refcount_c11.c',
  'refcount_lock.c',
  'refcount_win.c',
  'rsa_extra/rsa_asn1.c',
  'rsa_extra/rsa_crypt.c',
  'rsa_extra/rsa_print.c',
  'rsa_extra/rsassa_pss_asn1.c',
  'siphash/siphash.c',
  'spake25519/spake25519.c',
  'stack/stack.c',
  'thread.c',
  'thread_none.c',
  'thread_pthread.c',
  'thread_win.c',
  'trust_token/pmbtoken.c',
  'trust_token/trust_token.c',
  'trust_token/voprf.c',
  'ube/fork_ube_detect.c',
  'ube/ube.c',
  'ube/vm_ube_detect.c',
  'ui/ui.c',
  'x509/a_digest.c',
  'x509/a_sign.c',
  'x509/a_verify.c',
  'x509/algorithm.c',
  'x509/asn1_gen.c',
  'x509/by_dir.c',
  'x509/by_file.c',
  'x509/i2d_pr.c',
  'x509/name_print.c',
  'x509/policy.c',
  'x509/rsa_pss.c',
  'x509/t_crl.c',
  'x509/t_req.c',
  'x509/t_x509.c',
  'x509/t_x509a.c',
  'x509/v3_akey.c',
  'x509/v3_akeya.c',
  'x509/v3_alt.c',
  'x509/v3_bcons.c',
  'x509/v3_bitst.c',
  'x509/v3_conf.c',
  'x509/v3_cpols.c',
  'x509/v3_crld.c',
  'x509/v3_enum.c',
  'x509/v3_extku.c',
  'x509/v3_genn.c',
  'x509/v3_ia5.c',
  'x509/v3_info.c',
  'x509/v3_int.c',
  'x509/v3_lib.c',
  'x509/v3_ncons.c',
  'x509/v3_ocsp.c',
  'x509/v3_pcons.c',
  'x509/v3_pmaps.c',
  'x509/v3_prn.c',
  'x509/v3_purp.c',
  'x509/v3_skey.c',
  'x509/v3_utl.c',
  'x509/x509.c',
  'x509/x509_att.c',
  'x509/x509_cmp.c',
  'x509/x509_d2.c',
  'x509/x509_def.c',
  'x509/x509_ext.c',
  'x509/x509_lu.c',
  'x509/x509_obj.c',
  'x509/x509_req.c',
  'x509/x509_set.c',
  'x509/x509_trs.c',
  'x509/x509_txt.c',
  'x509/x509_v3.c',
  'x509/x509_vfy.c',
  'x509/x509_vpm.c',
  'x509/x509cset.c',
  'x509/x509name.c',
  'x509/x509rset.c',
  'x509/x509spki.c',
  'x509/x_algor.c',
  'x509/x_all.c',
  'x509/x_attrib.c',
  'x509/x_crl.c',
  'x509/x_exten.c',
  'x509/x_name.c',
  'x509/x_pubkey.c',
  'x509/x_req.c',
  'x509/x_sig.c',
  'x509/x_spki.c',
  'x509/x_val.c',
  'x509/x_x509.c',
  'x509/x_x509a.c',
)

asm_objects = []
fips_asm_objects = []
test_objects = []

win_asm = asm_opt.allowed() and (
  cc.get_id() == 'msvc'
  or (
    host_machine.system() == 'windows'
    and is_intel
  )
)

if win_asm
  nasm_args = [
    '-f',
    host_machine.cpu_family() == 'x86' ? 'win32' : 'win64',
    '-I',
    s2n_bignum_root / 'include',
    '-I',
    meson.project_source_root() / 'include',
  ]

  foreach file : asm_src
    asm_objects += custom_target(
      fs.stem(file),
      output: fs.stem(file) + '.obj',
      command: [
        nasm,
        nasm_args,
        '-o',
        '@OUTPUT@',
        '@INPUT@',
      ],
      input: file,
    )
  endforeach

  foreach file : fips_asm_src
    fips_asm_objects += custom_target(
      fs.stem(file),
      output: fs.stem(file) + '.obj',
      command: [
        nasm,
        nasm_args,
        '-o',
        '@OUTPUT@',
        '@INPUT@',
      ],
      input: file,
    )
  endforeach

  if gtest_dep.found()
    foreach file : test_asm_src
      test_objects += custom_target(
        fs.stem(file),
        output: fs.stem(file) + '.obj',
        command: [
          nasm,
          nasm_args,
          '-o',
          '@OUTPUT@',
          '@INPUT@',
        ],
        input: file,
      )
    endforeach
  endif

  unset_variable('asm_src')
  unset_variable('fips_asm_src')
  unset_variable('test_asm_src')

  asm_src = []
endif

bcm_asm_src = []

if asm_opt.allowed() and host_machine.cpu_family() == 'x86_64' and cc.get_argument_syntax() != 'msvc'
  asm_src += files('hrss/asm/poly_rq_mul.S')
endif

bcm_file = files('fipsmodule/bcm.c')
bcm_src = bcm_file

if cc.get_argument_syntax() != 'msvc'
  if host_machine.cpu_family() == 'x86_64'
    bcm_asm_src += files(
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/intt.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/mulcache_compute.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/ntt.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/nttfrombytes.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/ntttobytes.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/nttunpack.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/polyvec_basemul_acc_montgomery_cached_asm_k2.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/polyvec_basemul_acc_montgomery_cached_asm_k3.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/polyvec_basemul_acc_montgomery_cached_asm_k4.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/reduce.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/rej_uniform_asm.S',
      'fipsmodule/ml_kem/mlkem/native/x86_64/src/tomont.S',
    )
  elif host_machine.cpu_family() == 'aarch64'
    bcm_asm_src += files(
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/intt.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/ntt.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/poly_mulcache_compute_asm.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/poly_reduce_asm.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/poly_tobytes_asm.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/poly_tomont_asm.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/polyvec_basemul_acc_montgomery_cached_asm_k2.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/polyvec_basemul_acc_montgomery_cached_asm_k3.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/polyvec_basemul_acc_montgomery_cached_asm_k4.S',
      'fipsmodule/ml_kem/mlkem/native/aarch64/src/rej_uniform_asm.S',
    )

    asm_src += files('poly1305/poly1305_arm_asm.S')

    bcm_asm_src += arm_aes_xts
  elif host_machine.cpu_family() == 'arm'
    asm_src += files('poly1305/poly1305_arm_asm.S')
  endif
endif

src_fipsmodule = files(
  'fipsmodule/cpucap/cpucap.c',
  'fipsmodule/fips_shared_support.c',
)

fipsmodule_c_args = [
  '-DS2N_BN_HIDE_SYMBOLS',
]

private_c_args = ['-DBORINGSSL_IMPLEMENTATION']
public_c_args = []

private_c_args += common_args

if host_machine.system() == 'linux'
  private_c_args += ['-D_XOPEN_SOURCE=700']
endif

if host_machine.system() == 'windows'
  private_c_args += [
    '-DWIN32_LEAN_AND_MEAN',
    '-DNOMINMAX',
    '-D_CRT_SECURE_NO_WARNINGS',
  ]
endif

if default_library == 'shared'
  public_c_args += ['-DBORINGSSL_SHARED_LIBRARY']
endif

internal_args = []

if fips_opt.allowed()
  internal_args += ['-DBORINGSSL_FIPS']
endif

if asm_opt.disabled()
  internal_args += ['-DOPENSSL_NO_ASM']
endif

private_c_args += internal_args

cant_handle_asm = cc.get_id() == 'msvc' or (
  cc.get_id() == 'clang-cl'
  and host_machine.cpu_family() == 'aarch64'
) or (
  host_machine.system() == 'windows'
  and is_intel
)

if asm_opt.allowed() and not cant_handle_asm
  src += asm_src
  bcm_src += bcm_asm_src
  bcm_src += fips_asm_src
  bcm_src += bignum_asm_src
endif

if not jitterentropy.found()
  private_c_args += ['-DDISABLE_CPU_JITTER_ENTROPY']
endif

if fips_opt.allowed()
  marker_src = files('fipsmodule/fips_shared_library_marker.c')

  fipsmodule = static_library(
    'fipsmodule',
    src_fipsmodule,
    c_args: [private_c_args, public_c_args, fipsmodule_c_args],
    include_directories: inc,
    install: false,
    # Code must be PIC, any relocations can cause the executable to mutate
    # when loaded, invalidating the FIPS hash.
    pic: true,
    gnu_symbol_visibility: 'hidden',
    # Very load bearing, if debug info is included into the final binary
    # it will become unusable if ever stripped.
    override_options: ['debug=false'],
  )

  if host_machine.system() == 'linux'
    if default_library == 'shared'
      libcrypto_body = static_library(
        'crypto-awslc-body',
        src,
        err_data,
        c_args: [private_c_args, public_c_args],
        include_directories: inc,
        install: false,
        pic: true,
        gnu_symbol_visibility: 'hidden',
        override_options: ['debug=false'],
      )

      bcm_library = static_library(
        'bcm_library',
        bcm_src,
        c_args: [private_c_args, public_c_args, fipsmodule_c_args],
        include_directories: [inc, s2n_bignum_inc],
        install: false,
        pic: true,
        gnu_symbol_visibility: 'hidden',
        override_options: ['debug=false'],
      )

      ld = find_program(cc.get_linker_id())
      ldscript = cc.get_id() == 'gcc' ? 'fipsmodule/gcc_fips_shared.lds' : 'fipsmodule/fips_shared.lds'
      fips_bcm_module = custom_target(
        'fips_bcm_module',
        output: 'fips_bcm_module.o',
        command: [
          ld,
          '-r',
          '-T',
          meson.current_source_dir() / ldscript,
          '-o',
          '@OUTPUT@',
          '--whole-archive',
          bcm_library.full_path(),
        ],
        depends: [bcm_library],
      )

      libcrypto_awslc_prelink = shared_library(
        'crypto-awslc-prelink',
        c_args: private_c_args,
        link_whole: [
          libcrypto_body,
          fipsmodule,
        ],
        objects: [fips_bcm_module],
        dependencies: [jitterentropy, wsa2_dep],
        include_directories: inc,
        gnu_symbol_visibility: 'hidden',
        override_options: ['debug=false'],
        # Due to FIPS hash injection we need to mutate this library. If we
        # don't provide an soname with the final name then the linker will
        # link to the pre-hash injected version of library, which will cause
        # the FIPS POST checks to fail.
        link_args: ['-Wl,-soname,libcrypto-awslc.so'],
      )

      libcrypto_awslc = custom_target(
        'libcrypto-awslc',
        output: 'libcrypto-awslc.so',
        command: [
          custom_target_sh,
          # WORKDIR
          meson.project_source_root(),
          # INPUT
          libcrypto_awslc_prelink.full_path(),
          # OUTPUT
          meson.current_build_dir() / 'libcrypto-awslc.so',
          # CMD
          inject_hash_src[0].full_path(),
          # TYPE
          '-in-object',
        ],
        depends: libcrypto_awslc_prelink,
        install: true,
        install_dir: get_option('libdir'),
      )
    elif default_library == 'static'
      compiler = find_program(cc.get_id())
      ar = find_program('ar')

      bcm_generated_asm = custom_target(
        'bcm_generated_asm',
        output: 'bcm-generated.S',
        input: bcm_file,
        command: [
          compiler,
          '-S',
          '-P',
          '-fPIC',
          '-x',
          'c',
          '@INPUT@',
          '-o',
          '@OUTPUT@',
          private_c_args,
          public_c_args,
          fipsmodule_c_args,
          '-I',
          meson.project_source_root() / 'include',
          '-I',
          s2n_bignum_root / 'include',
        ],
      )

      bcm_generated_asm_archive = custom_target(
        'bcm_generated_asm_archive',
        output: 'bcm-generated-asm.a',
        input: bcm_generated_asm,
        command: [
          ar,
          'rcs',
          '@OUTPUT@',
          '@INPUT@',
        ],
      )

      delocator = custom_target(
        'delocator',
        output: 'delocator',
        command: [
          run_go,
          meson.project_source_root(),
          'go',
          'build',
          '-o',
          meson.current_build_dir() / 'delocator',
          'github.com/aws/aws-lc/util/fipstools/delocate',
        ],
      )

      bcm_delocated = custom_target(
        'bcm_delocated',
        input: bcm_asm_src + bignum_asm_src + fips_asm_src,
        output: 'bcm-delocated.S',
        command: [
          delocator,
          '-a',
          bcm_generated_asm_archive.full_path(),
          '-o',
          '@OUTPUT@',
          '-cc',
          cc.get_id(),
          '-cc-flags',
          '-DS2N_BN_HIDE_SYMBOLS',
          '-s2n-bignum-include',
          s2n_bignum_root / 'include',
          meson.project_source_root() / 'include/openssl/arm_arch.h',
          meson.project_source_root() / 'include/openssl/asm_base.h',
          meson.project_source_root() / 'include/openssl/target.h',
          '@INPUT@',
        ],
        depends: [delocator, bcm_generated_asm_archive],
      )

      bcm_hashunset = static_library(
        'bcm_hashunset',
        bcm_delocated,
        c_args: [private_c_args, public_c_args, fipsmodule_c_args],
        dependencies: [jitterentropy],
        include_directories: [inc, s2n_bignum_inc],
        install: false,
        gnu_symbol_visibility: 'hidden',
        override_options: ['debug=false'],
      )

      # Convert the thin archive into a regular one so inject_hash can
      # process it correctly
      fat_bcmhashunset = custom_target(
        'fat_bcmhashunset',
        output: 'fat_bcmhashunset.a',
        input: bcm_hashunset,
        command: [
          convert_archive_sh,
          '@INPUT@',
          '@OUTPUT@',
        ],
      )

      bcm_o_target = custom_target(
        'bcm_o_target',
        output: 'bcm.o',
        command: [
          run_go,
          meson.project_source_root(),
          'go',
          'run',
          inject_hash_src[0].full_path(),
          '-o',
          meson.current_build_dir() / 'bcm.o',
          '-in-archive',
          fat_bcmhashunset.full_path(),
        ],
        depends: fat_bcmhashunset,
      )

      libcrypto_awslc_body = static_library(
        'crypto-awslc-body',
        src,
        src_fipsmodule,
        err_data,
        c_args: [private_c_args, public_c_args],
        dependencies: [jitterentropy],
        include_directories: inc,
        install: false,
        gnu_symbol_visibility: 'hidden',
      )

      libcrypto_awslc = static_library(
        'crypto-awslc',
        link_whole: [
          libcrypto_awslc_body,
          fipsmodule,
        ],
        objects: [bcm_o_target],
        dependencies: [jitterentropy],
        include_directories: inc,
        install: true,
        gnu_symbol_visibility: 'hidden',
      )
    endif
  elif host_machine.system() == 'darwin'
    libcrypto_body = static_library(
      'crypto-awslc-body',
      src,
      err_data,
      c_args: [private_c_args, public_c_args],
      include_directories: inc,
      install: false,
      pic: true,
      override_options: ['debug=false'],
    )

    bcm_library = static_library(
      'bcm_library',
      bcm_src,
      c_args: [private_c_args, public_c_args, fipsmodule_c_args],
      include_directories: [inc, s2n_bignum_inc],
      install: false,
      pic: true,
      override_options: ['debug=false'],
    )

    fips_start_object = static_library(
      'fips_start_object',
      marker_src,
      c_args: [private_c_args, public_c_args, '-DAWSLC_FIPS_SHARED_START'],
      include_directories: inc,
      install: false,
      pic: true,
      override_options: ['debug=false'],
    )

    fips_end_object = static_library(
      'fips_end_object',
      marker_src,
      c_args: [private_c_args, public_c_args, '-DAWSLC_FIPS_SHARED_END'],
      include_directories: inc,
      install: false,
      pic: true,
      override_options: ['debug=false'],
    )

    libcrypto_awslc_prelink = shared_library(
      'crypto-awslc-prelink',
      c_args: private_c_args,
      # The order of these libraries is important, the bcm_library must be
      # enclosed by the start and end markers to ensure the FIPS hash
      # calculation is correct.
      link_whole: [
        fips_start_object,
        bcm_library,
        fips_end_object,
        fipsmodule,
        libcrypto_body,
      ],
      dependencies: [jitterentropy, wsa2_dep],
      include_directories: inc,
      override_options: ['debug=false'],
    )

    libcrypto_awslc = custom_target(
      'libcrypto-awslc',
      output: 'libcrypto-awslc.so',
      command: [
        custom_target_sh,
        # WORKDIR
        meson.project_source_root(),
        # INPUT
        libcrypto_awslc_prelink.full_path(),
        # OUTPUT
        meson.current_build_dir() / 'libcrypto-awslc.so',
        # CMD
        inject_hash_src[0].full_path(),
        # TYPE
        '-in-object',
      ],
      depends: libcrypto_awslc_prelink,
      install: true,
      install_dir: get_option('libdir'),
    )
  elif host_machine.system() == 'windows'
    bcm_library = static_library(
      'bcm_library',
      bcm_src,
      c_args: [private_c_args, public_c_args, fipsmodule_c_args],
      include_directories: [inc, s2n_bignum_inc],
      objects: [fips_asm_objects],
      dependencies: [wsa2_dep, jitterentropy],
      install: false,
      pic: true,
      gnu_symbol_visibility: 'hidden',
      override_options: ['debug=false'],
    )

    fips_start_object = static_library(
      'fips_start_object',
      marker_src,
      c_args: [private_c_args, public_c_args, '-DAWSLC_FIPS_SHARED_START'],
      include_directories: inc,
      install: false,
      pic: true,
      gnu_symbol_visibility: 'hidden',
      override_options: ['debug=false'],
    )

    fips_end_object = static_library(
      'fips_end_object',
      marker_src,
      c_args: [private_c_args, public_c_args, '-DAWSLC_FIPS_SHARED_END'],
      include_directories: inc,
      install: false,
      pic: true,
      gnu_symbol_visibility: 'hidden',
      override_options: ['debug=false'],
    )

    bcm_o_target = static_library(
      'bcm_o_target',
      pic: true,
      link_whole: [
        fips_start_object,
        bcm_library,
        fips_end_object,
      ],
    )

    libcrypto_body = static_library(
      'crypto-awslc-body',
      src,
      err_data,
      c_args: [private_c_args, public_c_args],
      objects: [asm_objects],
      dependencies: [wsa2_dep],
      include_directories: inc,
      install: false,
      pic: true,
      gnu_symbol_visibility: 'hidden',
      override_options: ['debug=false'],
    )

    precrypto = library(
      'precrypto',
      # HELLO! do not touch the order of these link_whole parameters
      # they MUST be the same as libcrypto_awslc, otherwise the fips
      # module hash will be invalid. Same goes from the dependencies
      # section.
      link_whole: [
        libcrypto_body,
        fipsmodule,
        bcm_o_target,
      ],
      dependencies: [jitterentropy, wsa2_dep],
      include_directories: inc,
      install: true,
      pic: true,
      gnu_symbol_visibility: 'hidden',
    )

    fips_empty_main = executable(
      'fips_empty_main',
      'fipsmodule/fips_empty_main.c',
      c_args: [private_c_args, public_c_args],
      include_directories: inc,
      install: false,
      override_options: ['debug=false'],
      link_with: precrypto,
      dependencies: [jitterentropy, wsa2_dep],
    )

    capture_hash = custom_target(
      'capture_hash',
      output: 'generated_fips_shared_support.c',
      command: [
        capture_hash_wrapper_sh,
        meson.project_source_root(),
        fips_empty_main,
        capture_hash_src[0].full_path(),
      ],
      depends: [fips_empty_main],
      capture: true,
    )

    generated_fipsmodule = static_library(
      'generated_fipsmodule',
      capture_hash,
      'fipsmodule/cpucap/cpucap.c',
      c_args: [private_c_args, public_c_args, fipsmodule_c_args],
      include_directories: inc,
      install: false,
      pic: true,
      gnu_symbol_visibility: 'hidden',
      override_options: ['debug=false'],
    )

    libcrypto_awslc = library(
      'crypto-awslc',
      # HELLO! do not touch the order of these link_whole parameters
      # they MUST be the same as precrypto, otherwise the fips
      # module hash will be invalid. Same goes from the dependencies
      # section.
      link_whole: [
        libcrypto_body,
        generated_fipsmodule,
        bcm_o_target,
      ],
      dependencies: [jitterentropy, wsa2_dep],
      include_directories: inc,
      install: true,
      pic: true,
      gnu_symbol_visibility: 'hidden',
    )
  endif
else
  # In non-FIPS mode aws-lc still links against the bcm and fipsmodule
  # libraries for functionality.
  if host_machine.system() in ['linux', 'darwin']
    fipsmodule = static_library(
      'fipsmodule',
      src_fipsmodule,
      c_args: [private_c_args, public_c_args, fipsmodule_c_args],
      include_directories: inc,
      install: false,
      gnu_symbol_visibility: 'hidden',
    )

    libbcm = static_library(
      'bcm',
      bcm_src,
      c_args: [private_c_args, public_c_args, fipsmodule_c_args],
      include_directories: [inc, s2n_bignum_inc],
      dependencies: [wsa2_dep, jitterentropy],
      install: false,
      gnu_symbol_visibility: 'hidden',
    )

    libcrypto_awslc = library(
      'crypto-awslc',
      src,
      err_data,
      c_args: [private_c_args, public_c_args],
      link_whole: [fipsmodule, libbcm],
      dependencies: [wsa2_dep, jitterentropy],
      include_directories: inc,
      install: true,
      gnu_symbol_visibility: 'hidden',
    )
  elif host_machine.system() == 'windows'
    fipsmodule = static_library(
      'fipsmodule',
      src_fipsmodule,
      c_args: [private_c_args, public_c_args, fipsmodule_c_args],
      include_directories: inc,
      install: false,
      gnu_symbol_visibility: 'hidden',
    )

    libbcm = static_library(
      'bcm',
      bcm_src,
      c_args: [private_c_args, public_c_args, fipsmodule_c_args],
      objects: [asm_objects, fips_asm_objects],
      include_directories: [inc, s2n_bignum_inc],
      dependencies: [wsa2_dep, jitterentropy],
      install: false,
      gnu_symbol_visibility: 'hidden',
    )

    libcrypto_awslc = library(
      'crypto-awslc',
      src,
      err_data,
      c_args: [private_c_args, public_c_args],
      link_whole: [fipsmodule, libbcm],
      dependencies: [wsa2_dep, jitterentropy],
      include_directories: inc,
      install: true,
      gnu_symbol_visibility: 'hidden',
    )
  endif
endif

libcrypto_dep = declare_dependency(
  link_with: libcrypto_awslc,
  dependencies: [wsa2_dep],
  include_directories: inc,
  compile_args: public_c_args,
  variables: ['OPENSSL_IS_AWS_LC=1'],
)

meson.override_dependency('libcrypto-awslc', libcrypto_dep)

libcrypto_testcases = {
  'abi_self_test': {
    'sources': files('abi_self_test.cc'),
  },
  'base64_test': {
    'sources': files('base64/base64_test.cc'),
  },
  'bio_md_test': {
    'sources': files('bio/bio_md_test.cc'),
  },
  'bio_socket_test': {
    'sources': files('bio/bio_socket_test.cc'),
  },
  'bio_test': {
    'sources': files('bio/bio_test.cc'),
  },
  'blake2_test': {
    'sources': files('blake2/blake2_test.cc'),
  },
  'buf_test': {
    'sources': files('buf/buf_test.cc'),
  },
  'bytestring_test': {
    'sources': files('bytestring/bytestring_test.cc'),
  },
  'chacha_test': {
    'sources': files('chacha/chacha_test.cc'),
  },
  'aead_test': {
    'sources': files('cipher_extra/aead_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'cipher_test': {
    'sources': files('cipher_extra/cipher_test.cc'),
    'timeout': 3000,
    'suite': 'slow',
  },
  'compiler_test': {
    'sources': files('compiler_test.cc'),
  },
  'conf_test': {
    'sources': files('conf/conf_test.cc'),
  },
  'console_test': {
    'sources': files('console/console_test.cc'),
  },
  'constant_time_test': {
    'sources': files('constant_time_test.cc'),
  },
  'crypto_test': {
    'sources': files('crypto_test.cc'),
  },
  'blowfish_test': {
    'sources': files('decrepit/blowfish/blowfish_test.cc'),
  },
  'cast_test': {
    'sources': files('decrepit/cast/cast_test.cc'),
  },
  'cfb_test': {
    'sources': files('decrepit/cfb/cfb_test.cc'),
  },
  'decrepit_evp_test': {
    'sources': files('decrepit/evp/evp_test.cc'),
  },
  'ripemd_test': {
    'sources': files('decrepit/ripemd/ripemd_test.cc'),
  },
  'des_test': {
    'sources': files('des/des_test.cc'),
  },
  'dh_extra_test': {
    'sources': files('dh_extra/dh_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'digest_extra_test': {
    'sources': files('digest_extra/digest_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'dsa_test': {
    'sources': files('dsa/dsa_test.cc'),
  },
  'ecdh_extra_test': {
    'sources': files('ecdh_extra/ecdh_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'endian_test': {
    'sources': files('endian_test.cc'),
  },
  'err_test': {
    'sources': files('err/err_test.cc'),
  },
  'evp_extra_test': {
    'sources': files('evp_extra/evp_extra_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'evp_test': {
    'sources': files('evp_extra/evp_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'mldsa_test': {
    'sources': files('evp_extra/mldsa_test.cc'),
  },
  'p_kem_test': {
    'sources': files('evp_extra/p_kem_test.cc'),
  },
  'p_pqdsa_test': {
    'sources': files('evp_extra/p_pqdsa_test.cc'),
  },
  'scrypt_test': {
    'sources': files('evp_extra/scrypt_test.cc'),
  },
  'fips_callback_test': {
    'sources': files('fips_callback_test.cc'),
  },
  'aes_test': {
    'sources': files('fipsmodule/aes/aes_test.cc'),
  },
  'bn_assert_test': {
    'sources': files('fipsmodule/bn/bn_assert_test.cc'),
  },
  'bn_test': {
    'sources': files('fipsmodule/bn/bn_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'cmac_test': {
    'sources': files('fipsmodule/cmac/cmac_test.cc'),
  },
  'cpu_aarch64_dit_test': {
    'sources': files('fipsmodule/cpucap/cpu_aarch64_dit_test.cc'),
  },
  'cpu_arm_linux_test': {
    'sources': files('fipsmodule/cpucap/cpu_arm_linux_test.cc'),
  },
  'ed25519_test': {
    'sources': files('fipsmodule/curve25519/ed25519_test.cc'),
  },
  'x25519_test': {
    'sources': files('fipsmodule/curve25519/x25519_test.cc'),
  },
  'ec_test': {
    'sources': files('fipsmodule/ec/ec_test.cc'),
  },
  'p256_nistz_test': {
    'sources': files('fipsmodule/ec/p256-nistz_test.cc'),
  },
  'ecdsa_test': {
    'sources': files('fipsmodule/ecdsa/ecdsa_test.cc'),
    'timeout': 300,
  },
  'evp_ctx_test': {
    'sources': files('fipsmodule/evp/evp_ctx_test.cc'),
  },
  'hkdf_test': {
    'sources': files('fipsmodule/hkdf/hkdf_test.cc'),
  },
  'kdf_test': {
    'sources': files('fipsmodule/kdf/kdf_test.cc'),
  },
  'md5_test': {
    'sources': files('fipsmodule/md5/md5_test.cc'),
  },
  'ml_kem_test': {
    'sources': files('fipsmodule/ml_kem/ml_kem_test.cc'),
  },
  'gcm_test': {
    'sources': files('fipsmodule/modes/gcm_test.cc'),
  },
  'xts_test': {
    'sources': files('fipsmodule/modes/xts_test.cc'),
    'timeout': 3000,
  },
  'pbkdf_test': {
    'sources': files('fipsmodule/pbkdf/pbkdf_test.cc'),
  },
  'cpu_jitter_test': {
    'sources': files('fipsmodule/rand/cpu_jitter_test.cc'),
    'dependencies': [jitterentropy],
  },
  'ctrdrbg_test': {
    'sources': files('fipsmodule/rand/ctrdrbg_test.cc'),
  },
  'entropy_source_test': {
    'sources': files('fipsmodule/rand/entropy/entropy_source_test.cc'),
  },
  'tree_drbg_jitter_entropy_isolated_test': {
    'sources': files(
      'fipsmodule/rand/entropy/tree_drbg_jitter_entropy_isolated_test.cc',
    ),
    'timeout': 300,
    'suite': 'slow',
  },
  'rand_isolated_test': {
    'sources': files('fipsmodule/rand/rand_isolated_test.cc'),
  },
  'rand_test': {
    'sources': files('fipsmodule/rand/rand_test.cc'),
  },
  'service_indicator_test': {
    'sources': files(
      'fipsmodule/service_indicator/service_indicator_test.cc',
    ),
    'timeout': 3000,
    'suite': 'slow',
  },
  'sha3_test': {
    'sources': files('fipsmodule/sha/sha3_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'sha_test': {
    'sources': files('fipsmodule/sha/sha_test.cc'),
  },
  'sshkdf_test': {
    'sources': files('fipsmodule/sshkdf/sshkdf_test.cc'),
  },
  'hpke_test': {
    'sources': files('hpke/hpke_test.cc'),
  },
  'hrss_test': {
    'sources': files('hrss/hrss_test.cc'),
  },
  'impl_dispatch_test': {
    'sources': files('impl_dispatch_test.cc'),
  },
  'lhash_test': {
    'sources': files('lhash/lhash_test.cc'),
  },
  'mem_set_test': {
    'sources': files('mem_set_test.cc'),
  },
  'mem_test': {
    'sources': files('mem_test.cc'),
  },
  'obj_test': {
    'sources': files('obj/obj_test.cc'),
  },
  'ocsp_test': {
    'sources': files('ocsp/ocsp_test.cc'),
  },
  'pem_test': {
    'sources': files('pem/pem_test.cc'),
  },
  'bio_cipher_test': {
    'sources': files('pkcs7/bio/bio_cipher_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'pkcs7_test': {
    'sources': files('pkcs7/pkcs7_test.cc'),
  },
  'pkcs12_test': {
    'sources': files('pkcs8/pkcs12_test.cc'),
  },
  'pkcs8_test': {
    'sources': files('pkcs8/pkcs8_test.cc'),
  },
  'poly1305_test': {
    'sources': files('poly1305/poly1305_test.cc'),
  },
  'pool_test': {
    'sources': files('pool/pool_test.cc'),
  },
  'ccrandomgeneratebytes_test': {
    'sources': files('rand_extra/ccrandomgeneratebytes_test.cc'),
  },
  'getentropy_test': {
    'sources': files('rand_extra/getentropy_test.cc'),
  },
  'urandom_test': {
    'sources': files('rand_extra/urandom_test.cc'),
  },
  'vm_ube_fallback_test': {
    'sources': files('rand_extra/vm_ube_fallback_test.cc'),
  },
  'refcount_test': {
    'sources': files('refcount_test.cc'),
  },
  'rsa_test': {
    'sources': files('rsa_extra/rsa_test.cc'),
    'timeout': 300,
    'suite': 'slow',
  },
  'rsassa_pss_asn1_test': {
    'sources': files('rsa_extra/rsassa_pss_asn1_test.cc'),
  },
  'rwlock_static_init_test': {
    'sources': files('rwlock_static_init.cc'),
  },
  'self_test': {
    'sources': files('self_test.cc'),
  },
  'siphash_test': {
    'sources': files('siphash/siphash_test.cc'),
  },
  'spake25519_test': {
    'sources': files('spake25519/spake25519_test.cc'),
  },
  'stack_test': {
    'sources': files('stack/stack_test.cc'),
  },
  'file_test_gtest': {
    'sources': files('test/file_test_gtest.cc'),
  },
  'malloc_test': {
    'sources': files('test/malloc.cc'),
  },
  'trust_token_test': {
    'sources': files('trust_token/trust_token_test.cc'),
    'timeout': 3000,
    'suite': 'slow',
  },
  'fork_ube_detect_test': {
    'sources': files('ube/fork_ube_detect_test.cc'),
  },
  'ube_test': {
    'sources': files('ube/ube_test.cc'),
  },
  'vm_ube_detect_test': {
    'sources': files('ube/vm_ube_detect_test.cc'),
  },
  'x509_tab_test': {
    'sources': files('x509/tab_test.cc'),
  },
  'x509_compat_test': {
    'sources': files('x509/x509_compat_test.cc'),
  },
  'x509_test': {
    'sources': files('x509/x509_test.cc'),
  },
  'x509_time_test': {
    'sources': files('x509/x509_time_test.cc'),
  },
}

if host_machine.system() != 'windows'
  libcrypto_testcases += {
    'thread_test': {
      'sources': files('thread_test.cc'),
    },
    'asn1_test': {
      'sources': files('asn1/asn1_test.cc'),
    },
  }
endif

private_c_args += cc.get_supported_arguments('-Wno-deprecated-declarations')

test_src = files(
  'test/abi_test.cc',
  'test/file_test.cc',
  'test/file_test_gtest.cc',
  'test/file_util.cc',
  'test/gtest_main.cc',
  'test/test_util.cc',
  'test/wycheproof_util.cc',
  'test/x509_util.cc',
)

if asm_opt.allowed() and not win_asm
  test_src += test_asm_src
endif

if gtest_dep.found()
  libtest_util = static_library(
    'aws_lc_test_util',
    test_src,
    crypto_test_data,
    include_directories: [inc, s2n_bignum_inc],
    dependencies: [gtest_dep, libcrypto_dep],
    cpp_args: [private_c_args],
    objects: [test_objects],
  )

  foreach testname, kwargs : libcrypto_testcases
    kwargs += {
      'dependencies': kwargs.get('dependencies', []) + [
        gtest_dep,
        libcrypto_dep,
      ],
    }

    exe = executable(
      testname,
      sources: kwargs['sources'],
      cpp_args: [private_c_args],
      dependencies: kwargs.get('dependencies', []),
      link_with: [libtest_util, libcrypto_awslc],
    )

    test(
      testname,
      exe,
      protocol: 'gtest',
      timeout: kwargs.get('timeout', 30),
      suite: kwargs.get('suite', 'libcrypto-awslc'),
    )
  endforeach
endif
