if get_option('default_library') != 'static'
  extra_args = ['-DFLAC_API_EXPORTS', '-DFLAC__USE_VISIBILITY_ATTR']
else
  extra_args = ['-DFLAC__NO_DLL']
endif

if cc.get_argument_syntax() == 'msvc' and cdata.get('WITH_AVX')
  extra_args += '/arch:AVX2'
endif

flac_lib = library(
  'FLAC',
  'bitmath.c',
  'bitreader.c',
  'bitwriter.c',
  'cpu.c',
  'crc.c',
  'fixed.c',
  'fixed_intrin_sse2.c',
  'fixed_intrin_ssse3.c',
  'float.c',
  'format.c',
  'lpc.c',
  'lpc_intrin_avx2.c',
  'lpc_intrin_fma.c',
  'lpc_intrin_neon.c',
  'lpc_intrin_sse2.c',
  'lpc_intrin_sse41.c',
  'lpc_intrin_vsx.c',
  'md5.c',
  'memory.c',
  'metadata_iterators.c',
  'metadata_object.c',
  'stream_decoder.c',
  'stream_encoder.c',
  'stream_encoder_intrin_sse2.c',
  'stream_encoder_intrin_ssse3.c',
  'stream_encoder_intrin_avx2.c',
  'stream_encoder_framing.c',
  'window.c',
  'ogg_decoder_aspect.c',
  'ogg_encoder_aspect.c',
  'ogg_helper.c',
  'ogg_mapping.c',
  win32_utf8,
  include_directories: [main_inc, 'include'],
  c_args: extra_args,
  version: '8.3.0',
  soversion: '8',
  dependencies: [ogg_dep, m_dep],
  install: true,
)

flac_dep = declare_dependency(
  compile_args: get_option('default_library') != 'static' ? [] : '-DFLAC__NO_DLL',
  include_directories: main_inc,
  link_with: flac_lib,
)

pconf.generate(
  flac_lib,
  name: 'flac',
  description: 'Free Lossless Audio Codec Library',
)
